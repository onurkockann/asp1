dataSeg SEGMENT PARA 'veri' ;dataSeg ISMINDEKI DATA SEGMENTIN BULUNDUGU BLOK VE TANIMLAMASI
    MSG1 DB 'KENAR DIZISININ ELEMAN SAYISINI GIRIN:',0    ;(BYTE BOYUTUNDA) KULLANICIDAN KENAR SAYISI ALINMADAN ONCE YAZDIRILAN MESAJIN TANIMLAMASI 
    MSG2 DB CR,LF,'KENAR GIRIN: ',0 ;(BYTE BOYUTUNDA) KULLANICIDAN ADAY KENAR ALINMADAN ONCE YAZDIRILAN MESAJIN TANIMLAMASI
    MSG3 DB CR,LF,'GIRILEN ADAY KENARLARIN DIZI SAYISI EN AZ 3-EN COK 100 ARALIGINDA OLMALIDIR!',0    ;(BYTE BOYUTUNDA) KULLANICININ GIRDIGI KENAR DIZISI SAYISI 
    ;ISTENILEN ARALIGIN DISINDA ISE
    ;YAZDIRILAN MESAJIN TANIMLAMASI
    MSG4 DB CR,LF,'->',0    ;(BYTE BOYUTUNDA)KENARLARI YAZDIRIRKEN '->' SEMBOLUYLE DAHA BELIRGIN HALE GETIRILMESI ICIN KULLANILAN DEGISKENIN TANIMLAMASI
    MSG5 DB CR,LF,'GIRILEN ADAY KENARLARIN DEGERI EN AZ 3-EN COK 1000 OLMALIDIR!',0    ;(BYTE BOYUTUNDA)KULLANICININ GIRDIGI ADAY KENAR SAYISI ARALIGIN DISINDA ISE 
    ;YAZDIRILACAK METININ TANIMLAMASI
    MSG6 DB CR,LF,'UCKEN KURULDU KURULAN UCGENIN KENARLARI:',0  ;(BYTE BOYUTUNDA)ADAY KENARLARDAN UCGEN KURULMA SARTI OLUSTUGU ZAMAN KENARLARDAN ONCE YAZDIRILACAK METININ TANIMLAMASI
    MSG7 DB CR,LF,'UCGEN KURULABILECEK ADAY KENAR YOK!',0   ;(BYTE BOYUTUNDA)ADAY KENARLARDAN UCGEN KURULMA SARTI OLUSMUYORSA YAZDIRILACAK METININ TANIMLAMASI
    K1 DW ? ;(WORD BOYUTUNDA)YAZDIRMA ISLEMINDEN ONCE VE KONTROL ISLEMLERINDE REGISTERLARDAKI IFADEYI YEDEKLEMEK AMACI ILE TUTULAN 1.KENARI TEMSILEN OLUSTURULAN DEGISKEN
    K2 DW ? ;(WORD BOYUTUNDA)YAZDIRMA ISLEMINDEN ONCE VE KONTROL ISLEMLERINDE REGISTERLARDAKI IFADEYI YEDEKLEMEK AMACI ILE TUTULAN 2.KENARI TEMSILEN OLUSTURULAN DEGISKEN
    K3 DW ? ;(WORD BOYUTUNDA)YAZDIRMA ISLEMINDEN ONCE VE KONTROL ISLEMLERINDE REGISTERLARDAKI IFADEYI YEDEKLEMEK AMACI ILE TUTULAN 3.KENARI TEMSILEN OLUSTURULAN DEGISKEN
    N DW 100    ;(WORD BOYUTUNDA)DIZININ ELEMAN SAYISINI TUTAN DEGISKEN.GECICI OLARAK 100 VERILMISTIR KOD ICERISINDE KULLANICIDAN KONTROLLERI VE DIZININ ELEMAN SAYISININ DEGERINI ALMA 
    ;VE TUTMA ISLEMLERINDE YENI(ASIL) DEGERINE SAHIP OLACAKTIR.
    DIZI DW 100 DUP(?)  ;(WORD BOYUTUNDA)ADAY KENAR UCGENLERININ TUTULACAGI DIZI
    ;ALT SATIRA GECMEK ICIN KULLANILAN DEGISKENLER(\n ISLEVI GIBI)
    CR EQU 13
    LF EQU 10
    HATA DB CR,LF,'DIKKAT!SAYIDAN HARIC BASKA BIR GIRIS YAPMAYIN', 0    ;SAYIDAN HARIC BASKA BIR INPUT DEGERI OKUNURSAKI CIKACAK MESAJIN
dataSeg ENDS    ;DATA SEGMENTIN BITISI

stackSeg SEGMENT PARA STACK 'yigin' ;stackSeg STACK SEGMENTIN BULUNDUGU BOLUM VE TANIMLAMASI
    DW 32 DUP(?)    ;32 WORDLUK STACKIN TANIMLAMASI
stackSeg ENDS

codeSeg SEGMENT PARA 'kod'  ;codeSeg ISMINDE CODE SEGMENTIN BASLADIGI SATIR
    ASSUME CS:codeSeg,DS:dataSeg,SS:stackSeg    ;SEGMENTLERIN ISIMLERININ TANITILMASI

BASLA PROC FAR  ;EXE TIPI PROGRAMLARDA GEREKLI OLAN BIR ADET FAR TIPINDEKI ANA PROSEDURUN(BASLA ISMINDE) BASLANGICI
    ;DONUS ICIN GEREKLI OLAN DEGERLERI YIGINDA SAKLAMA ISLEMI
    PUSH DS
    XOR AX,AX
    PUSH AX
    ;dataSeg ISMIYLE TANIMLI KESIM ALANINA ERISEBILMEK ICIN GEREKLI TANIMLAMA
    MOV AX,dataSeg
    MOV DS,AX  
    
    ;ODEV SORU:2NIN COZUMUNUN BASLADIGI ASIL YER.
    
    ;ILK OLARAK KULLANICIDAN BIR N DEGERI ALINIR 3 ILA 100 ARASINDAMI DIYE KIYASLANIR(N=DIZININ BOYUT SAYISI)
    XOR SI,SI
    MOV AX,OFFSET MSG1
    CALL PUT_STR;MSG1I GOSTEREN PROSEDURUN CAGIRILMASI
    CALL GETN;SAYIYI OKUYAN PROSEDURUN CAGIRILMASI
    MOV N,AX ;PROSEDUR DONUSU ALINAN SAYI AXDE TUTULUR.BU DEGERI N(DIZININ ELEMAN SAYISI)E ATILMASI.
    
    CMP N,3 ;N 3DEN BUYUKMU
    JAE CONT1 ;BUYUK ISE 1000DEN KUCUKMU VE ESITMI DIYE KARSILASTIR 
    MOV AX,OFFSET MSG3 ;DEGIL ISE MSG3U AX E AT
    CALL PUT_STR ;AXDEKI DEGERIN EKRANA YAZILMASI
    JMP SON ;SARTLAR UYUSMADI.SON LABELINA ZIPLA  
    
    CONT1:CMP N,100 ;N 1000DEN KUCUKMU VE ESITMI
    JBE CONT2   ;KUCUK VE ESIT ISE DIZIYE DEGER ALMA ISLEMINE BASLAYAN LABELA(CONT2) ZIPLA
    MOV AX,OFFSET MSG3
    CALL PUT_STR
    JMP SON
    
    
    
    ;KENARLARIN DIZISINE DEGERLERI ALMA
    CONT2:
    LEA SI,DIZI
    MOV CX,N    ;N(DIZININ ELEMAN SAYISI)
    ;DIZININ ELEMAN SAYISI KADAR DONGUYE GIR VE DEGER AL
 L1:MOV AX,OFFSET MSG2
    CALL PUT_STR
    CALL GETN   ;KLAVYEDEN OKUNAN DEGER AXDE TUTULUR
    
    CMP AX,0    ;ALINAN ILK ADAY KENARIN DEGERI 0DAN BUYUK OLMALIDIR
    JA CONT3    ;BUYUK ISE IKINCI SARTI KONTROL EDEN LABEL'A(CONT3) ZIPLA
    MOV AX,OFFSET MSG5  ;SARTLAR UYUSMADI.SON LABELINA ZIPLA
    CALL PUT_STR
    JMP SON
    
    CONT3:CMP AX,1000   ;IKINCI KONTROL GIRILEN SAYI 1000E ESIT VE KUCUKMU
    JBE CONT4   ;KUCUK VE ESIT ISE DEGERI DIZININ SI.CI INDISINE AL (CONT4 LABELINA ZIPLA)
    ;GIRILEN KENAR 2 SARTADA UYMADIGI ICIN CX INDISI BIR AZALTILIP TEKRAR DONGUYE GIRER(SON ADIMI TEKRARLAR
    MOV AX,OFFSET MSG5
    CALL PUT_STR
    INC CX 
    LOOP L1
    
    
    
    
    CONT4:MOV [SI],AX
    ADD SI,2    ;SI'NIN DEGERINI 2 ARTTIR(SONRAKI INDISDEKI DEGERE ULASMAK ICIN)
    LOOP L1  
    
                              

    
    ;KENARLARIN DIZISINI KUCUKTEN BUYUGE SIRALAMA ISLEMI YAPILIYOR CUNKU BULUNAN ILK UCGENDE PROGRAM TAMAMLANICAK VE EN KUCUK CEVREYE SAHIP OLAN UCGEN BULUNMUS OLUNACAK
    ;CX'E DIZININ ELEMAN SAYISI ATILIR VE 1 EKSIGI KADAR ITERASYON YAPILIR
    MOV CX,N      
    DEC CX
    ;BUBBLESORT ALGORITMASININ YAPILDIGI BOLUMDUR.MEVCUT DEGER AXDE TUTULUR SIRADAKI DEGER DXDE TUTULUR VE KARSILASTIRILIR
ARA:
    MOV BX,CX
    XOR SI,SI
KARSILASTIR:
    MOV AX,DIZI[SI]
    MOV DX,DIZI[SI+2]
    CMP AX,DX
    ;MEVCUT INDISDEKI DEGER SONRAKI DEGERDEN KUCUK ESIT ISE YERINI DEGISTIRME VE DEGISTIRME LABELINA ZIPLA
    JBE DEGISTIRME
    ;BUYUK ISE BIR SONRAKI INDISDEKI DEGERI MEVCUT INDISDEKI DEGERLE DEGISTIR
    MOV DIZI[SI],DX
    MOV DIZI[SI+2],AX
    
    ;SI INDISINI ARTTIR DONGUYE DEVAM ET
DEGISTIRME: 
    ADD SI,2
    DEC BX
    JNZ KARSILASTIR

    LOOP ARA  
    
     
     ;KUCUKTEN BUYUGE SIRALANAN DIZI ICIN EN KUCUK CEVREYE SAHIP OLAN UCGEN OLMA SARTI
     ;A+B>C VE C-B<A NIN KONTROLUNE BASLANAN KOD BLOGU
     
    XOR AX,AX   ;MEVCUT INDISDEKI 1.KENARI TUTACAK
    XOR BX,BX   ;MEVCUT INDISDEKI 3.KENARI TUTACAK
    XOR DX,DX   ;MEVCUT INDISDEKI 2.KENARI TUTACAK
    
    XOR DI,DI   ;ITERASYON ICIN TUTULACAK INDISIN SIFIRLANMASI
    
    ;DONGUNUN DIZININ BOYUTUNDAN 2 EKSIGI KADAR ILERLEMESI ICIN GEREKLI ATAMALAR
    MOV CX,N
    SUB CX,2
    ;3 KENARINDA SIRALI ITERASYONU ICIN 3 ADET IC ICE LOOP BULUNUR
    ;ILK LOOP MEVCUT INDISDEKI 1.KENARI TUTAR,2.LOOP 2.KENARI VE 3.LOOP 3.KENARI TUTAR 
 L4:PUSH CX ;L4 LOOPUNUN DEGERI KAYBEDILMEMESI ICIN STACKE PUSHLANIR
    MOV AX,DIZI[DI] ;AX REGISTERINA DIZININ SININCI INDISINDEKI(KENAR 1) DEGERIN ATILMASI
    MOV K1,AX   ;AX REGISTERINDAKI 1.KENAR DEGERI K1 DEGISKENINE YEDEKLEME AMACI ATILIR
    ;DIZI ITERASYONU ICIN DI IKI ARTTIRILIR
    INC DI
    INC DI
    PUSH DI ;AX ICIN YAPILAN ITERASYONDAKI INDIS DEGERINI TUTAN DI AYRI OLARAK STACKE ATILIR(HER LOOP VE KENAR TUTULMASI ICIN DI REGISTERI KULLANILDI CUNKU)
    L5: PUSH CX
        MOV DX,DIZI[DI] ;2.KENARI DX YAZMACINA ATILMASI
        MOV K2,DX   ;2.KENARIN YEDEKLENMESI
        INC DI
        INC DI
        PUSH DI
        L6:PUSH CX 
        MOV BX,DIZI[DI] ;ARTTIRILAN DI DEGERINDEKI MEVCUT DEGERI BXE AT(3.KENAR)
        MOV K3,BX   ;3.KENARIN YEDEKLENMESI
        INC DI
        INC DI
        
        
        ;A=1.KENAR , B=2.KENAR , C=3.KENAR
        ; A+B>?C KONTROLUNUN YAPILDIGI KISMIN BASLANGICI
        ; KAYBEDILMEMESI ICIN (REGISTERLARDA YAPILAN ISLEMLERDEN DOLAYI DEGISEN DEGERLERI)YEDEKLENEN KENARLARIN REGISTERLARA TEKRAR ATILMASI
        MOV AX,K1
        MOV DX,K2
        MOV BX,K3
        
        ;DIZIDEN TASMA VE UCGENIN OLUSMAMA DURUMU KONTROLU(162-167)
        ;AYNI ZAMANDA DIZIDEKI SON INDISLERINDE KONTROL DURUMUDUR.PROGRAM DAHA ILERSINE GIDEMEZ VE STACKTEKI BOS DEGERLER ILE ISLEM YAPAMAZ
        CMP AX,0
        JZ TASTI    ;1.KENARIN DEGERI SIFIRMI.SIFIR ISE TASMISTIR VE BU DEGER ILE UCGEN KURULMAZ TASTI LABELINA ZIPLA
        CMP BX,0
        JZ TASTI    ;2.KENARIN DEGERI SIFIRMI.SIFIR ISE TASMISTIR VE BU DEGER ILE UCGEN KURULMAZ TASTI LABELINA ZIPLA
        CMP DX,0
        JZ TASTI    ;3.KENARIN DEGERI SIFIRMI.SIFIR ISE TASMISTIR VE BU DEGER ILE UCGEN KURULMAZ TASTI LABELINA ZIPLA 
        
        ;A+B>?C KONTROLU
        ADD AX,DX
        CMP AX,BX
        JA DIGERINEGEC
        ;ILK SARTI SAGLIYAMADI DEVAM LABELINDEN DIGER DURUMA GEC
        JMP DEVAM
        
        
        ;   C-B<?A KONTROLU
        DIGERINEGEC:
        MOV AX,K1
        MOV DX,K2
        MOV BX,K3
        
        SUB BX,DX
        CMP BX,AX
        JB UCGEN
        ;VE IKINCI SARTIDA SAGLIYAMADIGI ICIN DEVAM LABELINDEN IP KODU OKUMAYA DEVAM EDER DIGER DURUMUN KONTROLU BASLAR
        
        DEVAM:
        POP CX  ;EN ICTEKI(L6) DONGUNUN STACKE ATILAN DEGERI STACKTEN GERI CEKILIR
        LOOP L6
        POP DI  ;2.KENARIN MEVCUT DIZI INDISINI TUTAN DI DEGERI STACKTEN GERI CEKILIR
      POP CX  ;IKINCI DONGUNUN(L5) STACKE ATILAN DONGU SAYISI DEGERI GERI CEKILIR.
      LOOP L5
      POP DI ;1.KENARIN MEVCUT DIZI INDISINI TUTAN DI DEGERI STACKTEN GERI CEKILIR
  POP CX  ;ILK KENARI(EN KUCUK) TUTAN LOOP(L4)ICIN STACKE ATILAN DONGU SAYISININ DEGERI TEKRAR GERI CEKILIR
  LOOP L4
   
   
   ;TASTI LABELI UCGEN OLUSMAMA SARTI DURUMUNDA ISLER
   TASTI:
   MOV AX,OFFSET MSG7
   CALL PUT_STR
   JMP SON  ;UCGEN OLMAMA SARTINDAN SONRA PROGRAMIN SONLANMASI ICIN SON DIYE ADLANDIRILAN LABELA ZIPLAR
   
   
   ;UCGEN OLUSMA DURUMUNDA EKRANA YAZDIRILMA ISLEMININ YAPILMASI
   UCGEN:
        MOV AX,OFFSET MSG6  ;UCGENIN OLUSTUGU BILGISININ KULLANICIYA SOYLENDIGI DEGISKENIN AX REGISTERINA ATILMASI
        CALL PUT_STR    ;PUT_STR PROSEDURU ILE EKRANA YAZDIRILMASI
        
        MOV AX,OFFSET MSG4  ;BELIRGINLIK ICIN MSG4E TANIMLADIGIMIZ '->' ISARETINE SAHIP DEGISKENIN AXE ATILIP ILGILI PROSEDURU CAGIRIP EKRANA YAZDIRILMASI ISLEMI
        CALL PUT_STR
        
        MOV AX,K1   ;ILK KENARI TUTAN VE YEDEKLEMESINI YAPTIGIMIZ K1 DEGISKENINI AX REGISTERINA ATILMASI
        CALL PUTN   ;PUTN PROSEDURU SAYISAL DEGERLERI YAZDIRIR VE AX YAZMACINDAKI DEGERI YAZDIRIR
        
        ;AYNI ISLEMLERIN 2.KENAR ICIN GERCEKLENMESI
        MOV AX,OFFSET MSG4
        CALL PUT_STR
        
        MOV AX,K2
        CALL PUTN
        
        ;UCUNCU KENARIN EKRANA YAZDIRILMASI
        MOV AX,OFFSET MSG4
        CALL PUT_STR
        
        MOV AX,K3               
        CALL PUTN 

        JMP SON ;PROGRAMIN SONLANMASI AMACI ILE OLUSTURULMUS SON LABELINA ZIPLANILIR
        
        SON:    ;BASLA(MAIN) PROSEDURU ICERISINDE BIRCOK DEFA KULLANILAN SON LABELI PROGRAMI SONLANDIRMA(SONUNA GOTURME/ZIPLAMA)AMACI ILE OLUSTURULMUSTUR

BASLA ENDP   


GETC PROC NEAR;KLAVYEDEN BASILAN KARAKTERI AL REGISTERINA ALIR VE EKRANDA GOSTERIR.ISLEM SONUCUNDA SADECE AL ETKILENIR
                MOV AH,1H
                INT 21H
                RET
           GETC ENDP 
           
PUTC PROC NEAR;AL REGISTERINDAKI DEGERI EKRANDA GOSTERIR.DL VE AH DEGISIYOR.AX VE DX REGISTERLARININ DEGERLERI KORUMAK ICIN PUSH/POP YAPILIR
                PUSH AX
                PUSH DX
                MOV DL,AL
                MOV AH,2
                INT 21H
                POP DX
                POP AX
                RET
           PUTC ENDP 
           
GETN PROC NEAR  ;KLAVYEDEN GIRILEN SAYIYI OKUR AX REGISTERI UZERINDE DONDURUR.
                ;DX:SAYININ ISARETLI OLUP/OLMADIGINI BELIRLER.1(+),-1(-)DEMEK
                ;BL:HANE BILGISINI TUTAR
                ;CX:OKUNAN SAYININ ISLENMESI SIRASINDAKI ARA DEGERI TUTAR.
                ;AL:KLAVYEDEN OKUNAN KARAKTERI TUTAR
                ;AX ZATEN DONUS DEGERI OLARAK DEGISMEK DURUMUNDADIR.ANCAK DIGER REGISTERLARIN DEGERLERI ESKISINE DONER
                PUSH BX
                PUSH CX
                PUSH DX
               GETN_START:
                MOV DX,1    ;SAYIYI SIMDILIK POZITIF OLDUGU VARSAYILIR
                XOR BX,BX   ;OKUMA YAPMADIGI HANE 0 OLUR
                XOR CX,CX   ;ARA TOPLAM DEGERI DE 0DIR
               NEW:
                CALL GETC   ;KLAVYEDEKI ILK DEGERI AL'YE OKU
                CMP AL,CR
                JE FIN_READ ;ENTER TUSUNA BASILMIS ISE OKUMA BITER
                CMP AL,'-'  ;AL ,'-'MI GELDI KONTROLU
                JNE CTRL_NUM;GELEN 0-9 ARASINDA BIR SAYIMI KONTROLU
               NEGATIVE:
                MOV DX,-1   ;- BASILDI ISE SAYI NEGATIF,DX=1 OLUR
                JMP NEW     ;YENI HANEYI AL
               CTRL_NUM:
                CMP AL,'0'  ;SAYININ 0-9 ARASINDA OLDUGUNU KONTROL ET
                JB ERROR
                CMP AL,'9'  
                JA ERROR    ;DEGIL ISE HATA MESAJI VERILSIN
                SUB AL,'0'  ;RAKAM ALINDI HANEYI TOPLAMA DAHIL ET
                MOV BL,AL   ;BL'YE OKUNAN HANEYI KOY
                MOV AX,10   ;HANEYI EKLERKEN *10 YAPILACAK
                PUSH DX     ;MUL KOMUTU DX'I BOZAR ISARET ICIN SAKLANIR VE STACKE ATILIR
                MUL CX      ;DX:AX = AX*CX ISLEMI
                POP DX      ;ISARETI GERI AL
                MOV CX,AX   ;CXDEKI ARA DEGER *10 YAPILDI
                ADD CX,BX   ;OKUNAN HANEYI ARA DEGERE EKLE
                JMP NEW     ;KLAVYEDEN YENI BASILAN DEGERI AL
                ERROR:
                MOV AX,OFFSET HATA
                CALL PUT_STR    ;HATA MESAJINI GOSTERIR
                JMP GETN_START  ;O ANA KADAR OKUNANLARI UNUTTUR VE YENIDEN SAYI ALMAYA BASLATIR
                FIN_READ:
                MOV AX,CX   ;SONUC AX UZERINDEN DONECEK 
                CMP DX,1    ;ISARETE GORE SAYIYI AYARLAMA ISLEMI YAPILIR
                JE FIN_GETN
                NEG AX      ;AX=-AX ISLEMI YAPILIR
                FIN_GETN:
                POP DX
                POP CX
                POP DX
                RET 
           GETN ENDP


PUTN PROC NEAR ; AX DE BULUNAN SAYIYI ONLUK TABANDA HANE HANE YAZDIRIR
               ;CX:HANELERI 10'A BOLEREK BULACAGIZ,CX=10 OLACAK
               ;DX:32 BOLMEDE ISLEME DAHIL OLACAK.SONUCU ETKILEMEMESI ICIN 0 OLMALIDIR.
                PUSH CX
                PUSH DX
                XOR DX,DX   ;DX 32 BIT BOLMEDE SONUCU ETKILEMESIN DIYE 0 OLMALI
                PUSH DX     ;HANELERI ASCII KARAKTER OLARAK STACKE SAKLAYACAGIZ VE KAC HANENIN ALINACAGINI BILMEDIGIMIZ ICIN 
                ;STACKE 0 DEGERINI KOYUP ONU ALANA KADAR DEVAM ETTIRILIR
                MOV CX,10   ;CX REGISTERINA 10 DEGERININ ATILMASI
                CMP AX,0
                JGE CALC_DIGITS
                NEG AX      ;SAYI NEGATIF ISE AX POZITIF YAPILIR
                PUSH AX     ;AX'I SAKLA
                MOV AL,'-'  ;ISARETI EKRANA YAZDIR
                CALL PUTC   
                POP AX      ;STACKTEKI AX REGISTERININ DEGERININ GERI ALINMASI
                CALC_DIGITS:
                DIV CX      ;DX:AX=AX/CX AX=BOLUMU TUTAR DX=KALANI TUTAR
                ADD DX,'0'  ;KALAN DEGERININ ASCII KARSILIGININ BULUNMASI
                PUSH DX     ;BU DEGER DX UZERINDEN STACKE ATILIR
                XOR DX,DX   ;DX SIFIRLANIR
                CMP AX,0    ;BOLEN 0 KALDI ISE SAYININ ISLENMESI SONA ERER.
                JNE CALC_DIGITS ;ISLEM TEKRARLANIR
                DISP_LOOP:  ;YAZILACAK TUM HANELERIMIZ YIGINDA.EN ANLAMLI HANE USTTE EN AZ ANLAMLI HANE EN ALTTA VE ONUN ALTINDA DA SONA VARDIGIMIZI
                            ;ANLAMAK ICIN KONULAN 0 DEGERI VAR.
                POP AX      ;SIRAYLA DEGERLER YIGINDAN ALINIR
                CMP AX,0    ;AX=0 ISE SONUNA GELINDI ANLAMINI TASIR.
                JE END_DISP_LOOP
                CALL PUTC   ;ALDEKI ASCII DEGERI YAZDIRIR
                JMP DISP_LOOP   ;ISLEME DEVAM EDILIR
                END_DISP_LOOP:
                POP DX
                POP CX
                RET
           PUTN ENDP   
           
PUT_STR PROC NEAR
            ;AX DE ADRESI VERILEN SONUNDA 0 OLAN DIZGEYI KARAKTER KARAKTER YAZDIRIR
            ;BX DIZIYE INDIS OLARAK KULLANILIR.ONCEKI DEGER SAKLANMALIDIR.
            PUSH BX
            MOV BX,AX   ;ADRES BX REGISTERINA ATILIR
            MOV AL,BYTE PTR [BX]    ;AL DE BYTE POINTERIN BX.CI INDISINI GOSTERDIGI ILK KARAKTER VAR
            PUT_LOOP:
                CMP AL,0
                JE PUT_FIN  ;0 GELDI ISE DIZI SONA ERER
                CALL PUTC   ;AL'DEKI KARAKTER EKRANA YAZDIRILIR
                INC BX
                MOV AL,BYTE PTR [BX]
                JMP PUT_LOOP;YAZDIRMAYA DEVAM EDILIR
                PUT_FIN:
                POP BX
                RET
            PUT_STR ENDP
codeSeg ENDS    ;codeSeg ISMINDEKI CODE SEGMENTIN BITISI
END BASLA